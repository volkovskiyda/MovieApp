plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlinx-serialization'
    id 'com.google.firebase.firebase-perf'
    id 'com.google.firebase.crashlytics'
}

def properties = new Properties()
properties.load(new FileInputStream(project.file("keystore.properties")))
properties.load(new FileInputStream(project.file("apikey.properties")))

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.gmail.volkovskiyda.movieapp"
        minSdkVersion 24
        targetSdkVersion 31
        versionCode 7
        versionName "1.5"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(properties['releaseKeyStore'])
            storePassword project.hasProperty('releaseStorePassword') ? project.properties['releaseStorePassword'] : properties['releaseStorePassword']
            keyAlias project.hasProperty('releaseKeyAlias') ? project.properties['releaseKeyAlias'] : properties['releaseKeyAlias']
            keyPassword project.hasProperty('releaseKeyPassword') ? project.properties['releaseKeyPassword'] : properties['releaseKeyPassword']
        }
        debug {
            storeFile file(properties['debugKeyStore'])
            keyAlias properties['debugKeyAlias']
        }
    }

    def apiKey = project.hasProperty('apiKey') ? project.properties['apiKey'] : properties['apiKey']
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField("String", "API_KEY", "\"$apiKey\"")
        }
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            buildConfigField("String", "API_KEY", "\"$apiKey\"")
        }
    }

    def javaVersion = JavaVersion.VERSION_11
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
        freeCompilerArgs = ["-Xallow-result-return-type"]
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"

    def navigation = "2.3.5"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation"

    implementation "io.coil-kt:coil:1.4.0"

    implementation platform("com.google.firebase:firebase-bom:26.4.0")
    implementation "com.google.firebase:firebase-firestore-ktx"
    implementation "com.google.firebase:firebase-storage-ktx"
    implementation "com.google.firebase:firebase-perf-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-analytics-ktx"

    def lifecycle = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    def ktor = "1.6.3"
    implementation "io.ktor:ktor-client-okhttp:$ktor"
    implementation "io.ktor:ktor-client-serialization-jvm:$ktor"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0"

    def room = "2.3.0"
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    kapt "androidx.room:room-compiler:$room"

    implementation "androidx.work:work-runtime-ktx:2.7.0"

    def hilt = "1.0.0"
    implementation "androidx.hilt:hilt-work:$hilt"
    kapt "androidx.hilt:hilt-compiler:$hilt"
}
