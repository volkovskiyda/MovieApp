plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlinx-serialization'
    id 'com.google.firebase.firebase-perf'
    id 'com.google.firebase.crashlytics'
}

def properties = new Properties()
properties.load(new FileInputStream(project.file("keystore.properties")))
properties.load(new FileInputStream(project.file("apikey.properties")))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.gmail.volkovskiyda.movieapp"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 5
        versionName "1.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias properties['releaseKeyAlias']
            keyPassword properties['releaseKeyPassword']
            storeFile file(properties['releaseKeyStore'])
            storePassword properties['releaseStorePassword']
        }
        debug {
            storeFile file(properties['debugKeyStore'])
            keyAlias properties['debugKeyAlias']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField("String", "API_KEY", "\"${properties['apiKey']}\"")
        }
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            buildConfigField("String", "API_KEY", "\"${properties['apiKey']}\"")
        }
    }

    def javaVersion = JavaVersion.VERSION_11
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
        freeCompilerArgs = ["-Xallow-result-return-type"]
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "com.google.android.material:material:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.recyclerview:recyclerview:1.1.0"

    def navigation = "2.3.4"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation"

    implementation "io.coil-kt:coil:1.1.1"

    implementation platform("com.google.firebase:firebase-bom:26.4.0")
    implementation "com.google.firebase:firebase-firestore-ktx"
    implementation "com.google.firebase:firebase-storage-ktx"
    implementation "com.google.firebase:firebase-perf-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-analytics-ktx"

    def lifecycle = "2.3.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    def ktor = "1.5.1"
    implementation "io.ktor:ktor-client-okhttp:$ktor"
    implementation "io.ktor:ktor-client-serialization-jvm:$ktor"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0"

    def room = "2.2.6"
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    kapt "androidx.room:room-compiler:$room"

    implementation "androidx.work:work-runtime-ktx:2.5.0"

    def hilt = "1.0.0-beta01"
    implementation "androidx.hilt:hilt-work:$hilt"
    kapt "androidx.hilt:hilt-compiler:$hilt"
}